//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Limited Ring enchants NPCs - Walkthrough Conversion.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

prontera,144,226,3	script	ออฟ Limited Ring	992,{
	disable_items;
	mes "[Byrnes]";
	mes "เจ้าเคยได้ยินเรื่อง Limited Ring ไหม ? เฉพาะผู้กล้าที่แท้จริงเท่านั้นที่จะได้รับแหวนที่ส่องประกาย, และข้าก็กำลังรอคนแบบนั้นอยู่.";
	mes "มีอะไรให้ข้าทำเพื่อเจ้าไหม ?";
	next;
	switch( select( "ออฟ Limited Ring.", "ลบออฟออกจาก Limited Ring.", "ยกเลิก." ) ) {
	case 1:
		mes "[Byrnes]";
		mes "ข้าสามารถออฟได้แต่แหวนเท่านั้น. แหวนอันไหนที่เจ้าต้องการออฟ ?";
		next;
		setarray .@eqi_slot[0], EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
		.@s = select( "Left", "Right" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[Byrnes]";
			mes "กรุณาสวม Limited Ring ถ้าเจ้าต้องการออฟ.";
			close;
		case 41029:
			break;
		default:
			mes "[Byrnes]";
			mes "กรุณาสวม Limited Ring ด้วย. ข้าไม่สามารถออฟแหวนอันอื่นได้.";
			close;
		}
		// custom at this point
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[0] > 0) {
			mes "[Byrnes]";
			mes "ออฟของ Limited Ring เต็มแล้วไม่สามารถออฟได้อีก.";
			close;
		}
		if (.@card[3] == 0) {
			setarray .@enchant_list$[0], "DEF", "MDEF";
			.@slot = 4;
		}
		else if (.@card[2] == 0) {
			setarray .@enchant_list$[0], "Max HP", "Max SP";
			.@slot = 3;
		}
		else if (.@card[1] == 0) {
			setarray .@enchant_list$[0], "Atk", "MAtk";
			.@slot = 2;
		}
		else {
			setarray .@enchant_list$[0], "STR", "INT", "AGI", "VIT", "DEX", "LUK";
			.@slot = 1;
		}
		mes "[Byrnes]";
		mes "เลือกความสามารถที่เจ้าต้องการจะออฟลงบนช่องที่ " + .@slot + "?";
		next;
		.@enchant_list_size = getarraysize(.@enchant_list$);
		for ( .@i = 0; .@i < .@enchant_list_size; ++.@i )
			.@menu$ += .@enchant_list$[.@i] + ":";
		.@type = select(.@menu$) - 1;

		switch(.@slot) {
		case 4:
			switch(.@type) {// enchant list index
			case 0:	// DEF
				setarray .@bonus[0], 4791, 4792, 4793, 4794, 4893;
				break;
			default:	// MDEF
				setarray .@bonus[0], 4890, 4786, 4891, 4787, 4892;
				break;
			}
			break;
		case 3:
			switch(.@type) {
			case 0:	// Max HP
				setarray .@bonus[0], 4861, 4862, 4867, 4868, 4900;
				break;
			default:	// Max SP
				setarray .@bonus[0], 4800, 4870, 4871, 4801, 4802;
				break;
			}
			break;
		case 2:
			switch(.@type) {
			case 0:	// Atk
				setarray .@bonus[0], 4882, 4766, 4767, 4894, 4895;
				break;
			default:	// Matk
				setarray .@bonus[0], 4883, 4896, 4897, 4898, 4899;
				break;
			}
			break;
		case 1:
			switch(.@type) {
			case 0:	// STR
				setarray .@bonus[0], 4700, 4701, 4702, 4703, 4704;
				break;
			case 1:	// INT
				setarray .@bonus[0], 4710, 4711, 4712, 4713, 4714; 
				break;
			case 2:	// AGI
				setarray .@bonus[0], 4730, 4731, 4732, 4733, 4734;
				break;
			case 3:	// VIT
				setarray .@bonus[0], 4740, 4741, 4742, 4743, 4744;
				break;
			case 4:	// DEX
				setarray .@bonus[0], 4720, 4721, 4722, 4723, 4724;
				break;
			default:	// LUK
				setarray .@bonus[0], 4750, 4751, 4752, 4753, 4754;
				break;
			}
			break;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 41029) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		.@bonus_size = getarraysize(.@bonus);
		.@card[.@slot-1] = .@bonus[ rand(.@bonus_size) ];	// unknown rates
		specialeffect2 EF_ANGEL2;
		mes "[Byrnes]";
		mes "การออฟสำเร็จ!";
		delequip .@eqi_slot[.@s];
		getitem2 41029,1,1,0,0,.@card[0],.@card[1],.@card[2],.@card[3];
		close;
	case 2:
		mes "[Byrnes]";
		mes "การลบออฟออกจาก Limited Ring, ข้าต้องการ  (Cash Coin 10) 5 อัน ในการลบ";
		next;
		if (countitem(41003) < 50) {
			mes "[Byrnes]";
			mes "เจ้ามี Cash Coin ไม่พอ. ข้าไม่สามารถลบออฟออกจาก Limited Ring ได้.";
			close;
		}
		mes "[Byrnes]";
		mes "ข้าสามารถลบออฟออกจากแหวนได้. เลือกแหวนที่เจ้าต้องการลบออฟ ?";
		next;
		setarray .@eqi_slot[0], EQI_SHADOW_ACC_L, EQI_SHADOW_ACC_R;
		.@s = select( "Left", "Right" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[Byrnes]";
			mes "เจ้าต้องสวมแหวนก่อนเสมอถ้าจะลบออฟ.";
			close;
		case 41029:
			break;
		default:
			mes "[Byrnes]";
			mes "กรุณาสวม Limited Ring ด้วย. ข้าไม่สามารถลบออฟออกจากแหวนอื่นได้.";
			close;
		}
		// custom at this point
		if (countitem(41003) < 5) {
			mes "[Byrnes]";
			mes "เจ้ามี Cash Coin ไม่พอ. ข้าไม่สามารถลบออฟออกจาก Limited Ring ได้.";
			close;
		}
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[3] == 0) {
			mes "[Byrnes]";
			mes "แหวน Limited Ring ของเจ้าตอนนี้มันไม่มีออฟเลย.";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 41029) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		specialeffect2 EF_ANGEL2;
		mes "[Byrnes]";
		mes "ทำการลบเรียบร้อย.";
		delitem 41002,5;
		delequip .@eqi_slot[.@s];
		getitem 41029,1;
		close;
	case 3:
		close;
		OnInit: 
	waitingroom "[ออฟ] Limited Ring",0; 
	end;
	}
}
